plugins {
    id 'java'
    id 'application'
    id "com.palantir.graal" version "0.6.0-102-ga30de19"
}

repositories {
    mavenCentral()
}

application {
    mainClassName = 'info.johtani.misc.cli.kuromoji.KuromojiCli'
}

group 'info.johtani.misc.cli'
version '0.11.0-SNAPSHOT'
description "Kuromoji CLI"

sourceCompatibility = 8
configurations {
    generateConfig
}

dependencies {
    compile "com.atilika.kuromoji:kuromoji-ipadic:${kuromojiVersion}"
    compile "com.atilika.kuromoji:kuromoji-unidic:${kuromojiVersion}"
    compile 'info.picocli:picocli:4.2.0'
    annotationProcessor 'info.picocli:picocli-codegen:4.2.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileJava {
    //for kuromoji dictionaries
    options.compilerArgs += [
            "-Aother.resource.patterns=.*.bin"
    ]
}

jar {
    manifest {
        attributes 'Main-Class': 'info.johtani.misc.cli.kuromoji.KuromojiCli'
    }
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}

graal {
    mainClass 'info.johtani.misc.cli.kuromoji.KuromojiCli'
    outputName 'kuromoji'
    graalVersion '19.2.0'
}